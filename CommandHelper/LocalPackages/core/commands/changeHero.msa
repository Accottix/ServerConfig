changehero:/changehero = >>>

    assign(@cooldown, 10)
  
    if(is_null(get_value('lasttransform')),
        store_value('lasttransform',0)
    )
    
    assign(@lastshot, get_value('lasttransform'))
    if(@lastshot + @cooldown > time() / 1000,
        die()
    )
    store_value('lasttransform', time() / 1000)
    
    set_block_at("-1733","67","225","152:0","greenfield")
    set_block_at("-1735","67","225","152:0","greenfield")
    
    set_timeout(2000, closure(){
        set_block_at("-1733","67","225","0:0","greenfield")
        set_block_at("-1735","67","225","0:0","greenfield")
    });
    
    set_timeout(1500, closure(){
        assign(@locOfRedBlock, array('x': '-1735', 'y': '69', 'z': '214', 'world': 'greenfield', 'yaw': '0', 'pitch': '0'))
        @plrs = players_in_radius(@locOfRedBlock,1)
        @count = array_size(@plrs)
        
        # Make sure only 1 player is in the machine.
        # If more than 1 person, kill everyone.
        if(@count == 0){
            die()
        }else if(@count > 1){
            @names = ''
            foreach(@player in @plrs){
                pkill(@player)
                @names += @player." and "
            }
            @names = substr(@names,0,length(@names) - 4)
            broadcast(@names."had their DNA mixed together, mutated, then died.")
            die()
        }
        
        # Get player name who is in the machine
        @plr = @plrs[0]
        
        # Check player inventory for DNA
        @inv = pinv(@plr)
        @haveDNA = false
        @class = ''
        @numberOfVials = 0
        foreach(@item in @inv)
        {
            if(@item != null && @item['meta'] != null && @item['meta']['display'] != null){
                @itemName = @item['meta']['display']
                if(length(@itemName) > 6){
                    @nameTest = substr(@itemName,length(@itemName) - 6,length(@itemName))
                    if(@nameTest == 'Sample'){
                        @class = @item['meta']['lore'][0]
                        @haveDNA = true
                        @numberOfVials++
                        if(@numberOfVials > 1){
                            set_pmode(@plr,"SURVIVAL")
                            runas(~console,/pex user @plr group set mortal)
                            set_peffect(@plr, 2, 3, 9999, true, false)
                            set_peffect(@plr, 9, 1, 9999, true, false)
                            set_peffect(@plr, 15, 1, 9999, true, false)
                            set_peffect(@plr, 17, 3, 9999, true, false)
                            set_peffect(@plr, 18, 3, 9999, true, false)
                            set_peffect(@plr, 19, 3, 9999, true, false)
                            include('/home/minecraft/server/plugins/CommandHelper/includes/changeSkin.ms')
                            _changeSkin(@plr,'mutant')
                            sudo("/minecraft:clear" @plr)
                            broadcast("§9@plr §amixed DNA samples and was turned into a deformed, freaky mutant!")
                            die()
                        }
                    }
                }
            }
        }
        
        if(!@haveDNA){
            tmsg(@plr,"You need some DNA to use this machine.")
            die()
        }
        
        # If player is trying to turn into OP hero and is not a mod, throw error
        include('/home/minecraft/server/plugins/CommandHelper/includes/getHeroInfo.ms')
        include('/home/minecraft/server/plugins/CommandHelper/includes/isClassOP.ms')
        if(_isClassOP(@class) == "true"){
            if(!has_permission(@plr,"rank.mod")){
                tmsg(@plr,"You can only win "._getHeroInfo(@class,0)['name']." §fby becoming staff or in a server tournament")
                die()
            }
        }
        
        # Make sure no other players are currently using the class you want
        # Only one person may be a given hero at a time
        @plrs = all_players()
        foreach(@player in @plrs)
        {
            @otherPlayerGroup = pgroup(@player)
            if(@otherPlayerGroup[0] == @class && @plr != @player){
                broadcast(
                    concat(color(white),@plr)
                    concat(color(gray),"tried to turn into")
                    _getHeroInfo(@class,0)['name']
                    concat(color(gray),"but")
                    concat(color(white),@player)
                    concat(color(gray),"was already that class!")
                )
                die()
            }
        }
        
        # 1 in 20 chance of machine going wrong
        @num = rand(1,21) 
        if(@num == 1){
            set_pmode(@plr,"SURVIVAL")
            runas(~console,/pex user @plr group set mortal)
            set_peffect(@plr, 2, 3, 9999, true, false)
            set_peffect(@plr, 9, 1, 9999, true, false)
            set_peffect(@plr, 15, 1, 9999, true, false)
            set_peffect(@plr, 17, 3, 9999, true, false)
            set_peffect(@plr, 18, 3, 9999, true, false)
            set_peffect(@plr, 19, 3, 9999, true, false)
            include('/home/minecraft/server/plugins/CommandHelper/includes/changeSkin.ms')
            _changeSkin(@plr,'mutant')
            sudo("/minecraft:clear" @plr)
            broadcast("§aSomething randomly went wrong with the §e§lD§a§lN§4§lA §amixer! §9@plr §awas turned into a deformed, freaky mutant!")
            die()
        }
        
        runas(~console, '/pex user' @plr 'group set' @class)
        sudo(/fly @plr off)
        sudo(/gamemode survival @plr)
        sudo(/effect @plr clear)
        sudo("/minecraft:clear" @plr)
        sudo(/god @plr off)
        
        # If hero has a trail, set it
        include('/home/minecraft/server/plugins/CommandHelper/includes/changeSkin.ms')
        _changeSkin(@plr,@class)
        
        # change player to hero skin
        include('/home/minecraft/server/plugins/CommandHelper/includes/changeTrail.ms')
        runas(~console,"/sudo ". @plr ." c:/trailsid none")
        _changeTrail(@plr, @class)
        
        # print chat message about successful transformation
        @fname = _getHeroInfo(@class,0)['name']
        if(@fname != 'false'){
            broadcast(
                concat(color(blue),@plr)
                concat(color(gray),"has transformed into")
                concat(@fname,concat(color(gray),"!"))
            )
        }
    });
    
<<<
