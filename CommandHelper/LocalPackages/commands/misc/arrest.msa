arrest:/arrest $plr $ = >>>

    @plr = $plr
    @plr = player(@plr)
    @reason = $
    @date = simple_date('hh:mm aaa z',time(),'CST')

    @cop = player()

    sudo('/sudo '.@plr.' c:/ch leave local')
    sudo('/sudo '.@plr.' c:/ch join global')
    include('/home/minecraft/server/plugins/CommandHelper/includes/mortal.ms')
    _mortal(@plr)
    broadcast(@plr.color(red).' HAS BEEN ARRESTED BY '.color(white).@cop.color(red).' FOR '.color(yellow).@reason)

    @vicUUID = puuid(@plr)

    @profile = array(type: 'sqlite', file: '/home/minecraft/server/plugins/CommandHelper/policedatabase.sqlite')

    @ip = pinfo(@plr)[3]
    @date = simple_date('yyyy-MM-dd HH:mm:ss', time())
    @result = query(@profile,'INSERT INTO `criminals` '.
                             'VALUES(?,?,?,?,?,?,?,?)',
                             @plr,@vicUUID,@reason,'ARREST',@ip,@cop,puuid(@cop),simple_date('yyyy-MM-dd HH:mm:ss', time())
    )

    set_timeout(3000,closure(
        sudo("/pex user ".@plr." group set criminal")
        sudo("/tptoggle ".@plr." off")
        sudo("/warp quarantine ".@plr)
    ))


<<<

release:/release $plr = >>>

    @plr = $plr
    @plr = player(@plr)
    sudo("/pex user ".@plr." group set mortal")
    sudo("/warp city ".@plr)
    sudo("/tptoggle ".@plr." on")

<<<

warn:/warn $plr $ = >>>

    @plr = $plr
    @plr = player(@plr)
    @reason = $
    @cop = player()
    @uuid = puuid(@plr)
    @ip = pinfo(@plr)[3]
    broadcast(color(bold).@plr.color(dark_red).color(bold).' HAS BEEN WARNED BY '.color(white).@cop.color(dark_red).color(bold).' ON THE GROUNDS OF '.color(yellow).@reason.color(red).color(bold).'. Please read '.color(dark_red).'/warp rules'.color(red).' and familiarize yourself with the server rules or you may end up jailed!')

    @profile = array(type: 'sqlite', file: '/home/minecraft/server/plugins/CommandHelper/policedatabase.sqlite')
    query(@profile,'INSERT INTO `criminals` '.
                   'VALUES(?,?,?,?,?,?,?,?)',
                   @plr,@uuid,@reason,'WARN',@ip,@cop,puuid(@cop),simple_date('yyyy-MM-dd HH:mm:ss', time())
    )
<<<

createdb:/createdb = >>>

    @profile = array(type: 'sqlite', file: '/home/minecraft/server/plugins/CommandHelper/policedatabase.sqlite')
    @result = query(@profile, 'CREATE TABLE criminals(
    PERPNAME       TEXT    NOT NULL,
    PERPUUID       TEXT    NOT NULL,
    REASON         TEXT    NOT NULL,
    TYPE           TEXT    NOT NULL,
    IP             TEXT    NOT NULL,
    AO             TEXT    NOT NULL,
    OFFUUID        TEXT    NOT NULL,
    DATE           TEXT    NOT NULL
)')

<<<

checkfingerprints:/checkfingerprints $plr = >>>

    @plr = $plr
    @plr = player(@plr)
    @ip = pinfo(@plr)[3]
    @cop = player()
    @profile = array(type: 'sqlite', file: '/home/minecraft/server/plugins/CommandHelper/policedatabase.sqlite')
    @result = query(@profile, 'SELECT DISTINCT PERPNAME,IP FROM `criminals` WHERE IP=?',@ip)
    foreach(@row in @result){
        tmsg(@cop,@row['PERPNAME'])
    }
<<<

backgroundcheck:/backgroundcheck $plr = >>>

    @plr = $plr
    @plr = player(@plr)
    @cop = player()
    @uuid = puuid(@plr)
    @ip = pinfo(@plr)[3]
    @profile = array(type: 'sqlite', file: '/home/minecraft/server/plugins/CommandHelper/policedatabase.sqlite')
    @result = query(@profile, 'SELECT * FROM `criminals` WHERE PERPUUID=?',@uuid)
    @numOffenses = array_size(@result)
    foreach(@row in @result){
        tmsg(@cop,
color(dark_red).'Offense Type: '.@row['TYPE'].color(yellow).' | '.
color(red).'Perp Name: '.@row['PERPNAME'].color(yellow).' | '.
color(gold).'Reason: '.@row['REASON'].color(yellow).' | '.
color(aqua).'Officer: '.color(dark_aqua).@row['AO'].color(yellow).' | '.
color(green).@row['DATE'])
        tmsg(@cop,'-----------------------------------------')
    }
    tmsg(@cop,'A total of '.@numOffenses.' warnings and offenses.')
<<<

officerhistory:/officerhistory $plr = >>>

    @plr = $plr
    @plr = player(@plr)
    @cop = player()
    @uuid = puuid(@plr)
    @ip = pinfo(@plr)[3]
    @profile = array(type: 'sqlite', file: '/home/minecraft/server/plugins/CommandHelper/policedatabase.sqlite')
    @result = query(@profile, 'SELECT * FROM `criminals` WHERE OFFUUID=?',@uuid)
    @numOffenses = array_size(@result)
    foreach(@row in @result){
        tmsg(@cop,
color(dark_red).'Offense Type: '.@row['TYPE'].color(yellow).' | '.
color(red).'Perp Name: '.@row['PERPNAME'].color(yellow).' | '.
color(gold).'Reason: '.@row['REASON'].color(yellow).' | '.
color(aqua).'Officer: '.color(dark_aqua).@row['AO'].color(yellow).' | '.
color(green).@row['DATE'])
        tmsg(@cop,'-----------------------------------------')
    }
    tmsg(@cop,'A total of '.@numOffenses.' warnings and offenses.')
<<<
